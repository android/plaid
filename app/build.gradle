/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../core_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.gitCommitCount
        versionName = '1.0.7'
        archivesBaseName = 'plaid'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfig 'en'
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""
        manifestPlaceholders += [
                crashlyticsEnabled: false
        ]
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        release {
            // There's a Dex Splitter issue when enabling DataBinding & proguard in dynamic features
            // The temporary workaround is to disable shrinking
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders += [crashlyticsEnabled: true]
        }
        debug {
            ext.alwaysUpdateBuildId = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dynamicFeatures = [':about', ':designernews', ':dribbble', ':search']
}

repositories {
    maven {
        url "http://maven.google.com"
        content {
            includeGroupByRegex "com\\.android.*"
            includeGroupByRegex "androidx.*"
            includeGroup "android.arch.lifecycle"
            includeGroup "android.arch.core"
            includeGroup "com.google.firebase"
            includeGroup "com.google.android.gms"
            includeGroup "com.google.android.material"
        }
    }
    maven {
        url 'https://jcenter.bintray.com'
        content {
            includeGroupByRegex "com\\.google.*"
            includeGroupByRegex "com\\.sun.*"
            includeGroupByRegex "com\\.squareup.*"
            includeGroupByRegex "com\\.jakewharton.*"
            includeGroupByRegex "com\\.googlecode.*"
            includeGroupByRegex "org\\.jetbrains.*"
            includeGroupByRegex "org\\.codehaus.*"
            includeGroupByRegex "org\\.apache.*"
            includeGroupByRegex "net\\.sf.*"
            includeGroupByRegex "javax.*"
            includeGroup "com.github.bumptech.glide"
            includeGroup "com.nhaarman.mockitokotlin2"
            includeGroup "commons-io"
            includeGroup "commons-codec"
            includeGroup "commons-logging"
            includeGroup "it.unimi.dsi"
            includeGroup "junit"
            includeGroup "net.bytebuddy"
            includeGroup "org.antlr"
            includeGroup "org.bouncycastle"
            includeGroup "org.ow2.asm"
            includeGroup "org.glassfish.jaxb"
            includeGroup "org.jvnet.staxex"
            includeGroup "org.jsoup"
            includeGroup "org.mockito"
            includeGroup "org.hamcrest"
            includeGroup "org.objenesis"
            includeGroup "org.checkerframework"

            excludeGroup "com.google.firebase"
            excludeGroup "com.google.android.gms"
            excludeGroup "com.google.android.material"
        }
    }
    maven {
        url 'https://maven.fabric.io/public'
        content {
            includeGroupByRegex "io\\.fabric.*"
            includeGroupByRegex "com\\.crashlytics.*"
        }
    }
}

dependencies {
    implementation project(':core')

    implementation "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}"
    implementation "com.google.firebase:firebase-core:${versions.firebase}"

    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
