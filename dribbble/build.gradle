/*
 *   Copyright 2018 Google LLC
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 */

apply plugin: 'com.android.dynamic-feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: '../core_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        def filesAuthorityValue = names.applicationId + '.shareprovider'
        buildConfigField 'String', 'FILES_AUTHORITY', "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        url "http://maven.google.com"
        content {
            includeGroupByRegex "com\\.android.*"
            includeGroupByRegex "androidx.*"
            includeGroup "android.arch.lifecycle"
            includeGroup "android.arch.core"
            includeGroup "com.google.android.material"
        }
    }
    maven {
        url 'https://jcenter.bintray.com'
        content {
            includeGroupByRegex "com\\.google.*"
            includeGroupByRegex "com\\.sun.*"
            includeGroupByRegex "com\\.squareup.*"
            includeGroupByRegex "com\\.jakewharton.*"
            includeGroupByRegex "com\\.googlecode.*"
            includeGroupByRegex "org\\.jetbrains.*"
            includeGroupByRegex "org\\.codehaus.*"
            includeGroupByRegex "org\\.apache.*"
            includeGroupByRegex "net\\.sf.*"
            includeGroupByRegex "javax.*"
            includeGroup "com.github.bumptech.glide"
            includeGroup "com.nhaarman.mockitokotlin2"
            includeGroup "commons-io"
            includeGroup "commons-codec"
            includeGroup "commons-logging"
            includeGroup "it.unimi.dsi"
            includeGroup "junit"
            includeGroup "net.bytebuddy"
            includeGroup "org.antlr"
            includeGroup "org.bouncycastle"
            includeGroup "org.checkerframework"
            includeGroup "org.glassfish.jaxb"
            includeGroup "org.hamcrest"
            includeGroup "org.jsoup"
            includeGroup "org.jvnet.staxex"
            includeGroup "org.mockito"
            includeGroup "org.objenesis"
            includeGroup "org.ow2.asm"

            excludeGroup "com.google.android.material"
        }
    }
    maven {
        url 'https://maven.fabric.io/public'
        content {
            includeGroupByRegex "io\\.fabric.*"
        }
    }
    maven {
        url 'http://dl.bintray.com/kotlin/kotlin-eap'
        content {
            includeGroupByRegex "org\\.jetbrains.*"
        }
    }
}

dependencies {
    implementation project(':app')
    implementation project(':core')

    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
}
